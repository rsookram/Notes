apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion Versions.targetSdk

    defaultConfig {
        applicationId "io.github.rsookram.notes"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 1
        versionName "1.0"

        resConfigs("en")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            signingConfig = signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'

            // Just for testing release builds. Not actually distributed.
            signingConfig = signingConfigs.debug
        }
    }

    signingConfigs {
        debug {
            storeFile = file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    packagingOptions {
        exclude("/kotlin/**")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.allWarningsAsErrors = true
}

dependencies {
    implementation Versions.appCompat
    implementation Versions.materialComponents
    implementation Versions.ktxCore

    implementation Versions.inboxRecyclerView

    implementation Versions.epoxyCore
    kapt Versions.epoxyProcessor

    implementation Versions.roomRuntime
    implementation Versions.roomKtx
    kapt Versions.roomCompiler

    implementation Versions.kotlinStdlib
    implementation Versions.kotlinCoroutinesCore
    implementation Versions.kotlinCoroutinesAndroid
}

kapt {
    // Needed for epoxy's annotation processor
    correctErrorTypes = true
}
